PROTOC_GEN_GO_VERSION := "v1.36.5"
GOBIN := `go env GOPATH`+ "/bin"
PROTOC_GEN_GO := GOBIN + "/protoc-gen-go"
PROTOC_GEN_CONNECT_GO := GOBIN + "/protoc-gen-connect-go"
PROTOC_GEN_DOC := GOBIN + "/protoc-gen-doc"

[private]
ensure-protoc-gen-go:
    #!/usr/bin/env bash
    if [ ! -f "{{PROTOC_GEN_GO}}" ] || [ ! "$({{PROTOC_GEN_GO}} --version)" = "protoc-gen-go {{PROTOC_GEN_GO_VERSION}}" ]; then
        echo "Please install protoc-gen-go {{PROTOC_GEN_GO_VERSION}}:"
        echo
        echo "  go install google.golang.org/protobuf/cmd/protoc-gen-go@{{PROTOC_GEN_GO_VERSION}}"
        echo
        exit 1
    fi

[private]
ensure-protoc-gen-connect-go:
    #!/usr/bin/env bash
    if [ ! -f "{{PROTOC_GEN_CONNECT_GO}}" ]; then
        echo "Please install protoc-gen-connect-go"
        echo
        echo " go install connectrpc.com/connect/cmd/protoc-gen-connect-go@latest"
        echo
        exit 1
    fi

fmt:
    buf format -w --path proto

buf-lint:
    buf lint --path proto

proto-gen: ensure-protoc-gen-go ensure-protoc-gen-connect-go
    buf generate --path ./proto

[private]
ensure-protoc-gen-doc:
    #!/usr/bin/env bash
    if [ ! -f "{{PROTOC_GEN_DOC}}" ]; then
        echo "Please install protoc-gen-doc:"
        echo
        echo "  go install github.com/pseudomuto/protoc-gen-doc/cmd/protoc-gen-doc@latest"
        echo
        exit 1
    fi

proto-docs out="docs": ensure-protoc-gen-doc
    mkdir -p {{out}}
    buf generate --path ./proto --template buf.gen.docs.yaml --output {{out}}

lint *args: buf-lint
